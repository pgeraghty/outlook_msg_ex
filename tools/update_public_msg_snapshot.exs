alias OutlookMsg.Mapi.PropertySet

fixtures_dir = Path.expand("../test/fixtures/public_msg", __DIR__)
snapshot_path = Path.expand("../data/snapshots/public_msg_snapshot.exs", __DIR__)

files =
  fixtures_dir
  |> Path.join("*.msg")
  |> Path.wildcard()
  |> Enum.sort()

if files == [] do
  IO.puts("No fixtures found in #{fixtures_dir}")
  System.halt(1)
end

normalize = fn v ->
  case v do
    nil ->
      ""

    x ->
      x
      |> to_string()
      |> String.replace("\u0000", "")
      |> String.replace(~r/\s+/, " ")
      |> String.trim()
  end
end

rows =
  for file <- files, into: %{} do
    {:ok, msg} = OutlookMsg.open(file)
    p = msg.properties

    body = PropertySet.body(p) || ""
    html = PropertySet.body_html(p) || ""
    body = if is_binary(body), do: body, else: inspect(body)
    html = if is_binary(html), do: html, else: inspect(html)

    first_rec = List.first(msg.recipients)
    first_rec_email = if first_rec, do: first_rec.email, else: nil

    row = %{
      subject: normalize.(PropertySet.subject(p)),
      recipient_count: length(msg.recipients),
      attachment_count: length(msg.attachments),
      first_recipient_email: normalize.(first_rec_email),
      body_len: byte_size(body),
      html_len: byte_size(html),
      body_sha256: :crypto.hash(:sha256, body) |> Base.encode16(case: :lower),
      html_sha256: :crypto.hash(:sha256, html) |> Base.encode16(case: :lower)
    }

    {Path.basename(file), row}
  end

content = """
# Generated by tools/update_public_msg_snapshot.exs
# Public fixtures only. Do not place private message content in this file.

%{
#{rows |> Enum.sort_by(fn {k, _} -> k end) |> Enum.map_join(",\n", fn {k, v} -> "  #{inspect(k)} => #{inspect(v, pretty: true, limit: :infinity)}" end)}
}
"""

File.write!(snapshot_path, content)
IO.puts("Wrote snapshot: #{snapshot_path}")
